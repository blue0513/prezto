# Copy from skwp and steeef

function prompt_blue0513_precmd {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    branch_format=" [${_prompt_blue0513_colors[1]}%b%f${_prompt_blue0513_colors[4]}%f]"
  else
    branch_format=" [${_prompt_blue0513_colors[1]}%b%f]"
  fi

  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"

  vcs_info 'prompt'
}

function prompt_blue0513_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info
  
  # Add hook to set up prompt parameters before each command.
  add-zsh-hook precmd prompt_blue0513_precmd

  # Use extended color pallete if available.
  _prompt_blue0513_colors=(
      "%F{81}"  # Turquoise
      "%F{166}" # Orange
      "%F{135}" # Purple
      "%F{161}" # Hotpink
      "%F{118}" # Limegreen
      "%F{226}" # Yellow
  )

  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  local branch_format="(${_prompt_blue0513_colors[1]}%b%f)"
  
  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""
  
  # Define prompts.
  PROMPT="${_prompt_blue0513_colors[3]}%~%f"'${vcs_info_msg_0_}'" ${_prompt_blue0513_colors[3]}‚ùØ%f "
}

prompt_blue0513_setup "$@"
